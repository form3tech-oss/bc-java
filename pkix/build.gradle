dependencies {
    compile project(':core')
    compile project(':util')
    compile project(':prov')
}

jar.baseName = "bcpkix-$vmrange"

task sourcesJar(type: Jar) {
    baseName = jar.baseName
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName = jar.baseName
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

group 'org.bouncycastle'

publishing {
    repositories {
        maven {
            name = "form3releases"
            url "s3://form3-internal/releases"
            credentials(AwsCredentials) {
                accessKey "${System.getenv("AWS_ACCESS_KEY_ID")}"
                secretKey "${System.getenv("AWS_SECRET_ACCESS_KEY")}"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId group
            artifact ("../build/artifacts/jdk1.5/jars/bcpkix-jdk15on-169.jar")
            artifactId "form3-bc-pkix"
            version project.version
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'org.bouncycastle')
                dependencyNode.appendNode('artifactId', 'bcprov-jdk15on')
                dependencyNode.appendNode('version', "$version")
                dependencyNode.appendNode('scope', 'compile')
                dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'org.bouncycastle')
                dependencyNode.appendNode('artifactId', 'bcutil-jdk15on')
                dependencyNode.appendNode('version', "$version")
                dependencyNode.appendNode('scope', 'compile')
            }
        }
    }
}